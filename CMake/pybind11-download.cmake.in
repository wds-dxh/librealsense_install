cmake_minimum_required(VERSION 3.6)
project(pybind11-download NONE)

include(ExternalProject)

# Check if the directory already exists before attempting download
if(NOT EXISTS "${CMAKE_BINARY_DIR}/third-party/pybind11")
    ExternalProject_Add(
        pybind11
        PREFIX .
        GIT_REPOSITORY "https://github.com/pybind/pybind11.git"
        GIT_TAG        v2.13.6
        GIT_CONFIG     advice.detachedHead=false  # otherwise we'll get "You are in 'detached HEAD' state..."
        SOURCE_DIR     "${CMAKE_BINARY_DIR}/third-party/pybind11"
        GIT_SHALLOW    ON        # No history needed (requires cmake 3.6)
        # Override default steps with no action, we just want the clone step.
        CONFIGURE_COMMAND ""
        BUILD_COMMAND ""
        INSTALL_COMMAND ""
        )
else()
    # If the directory exists, create a dummy external project to satisfy dependencies
    ExternalProject_Add(
        pybind11
        DOWNLOAD_COMMAND ""
        CONFIGURE_COMMAND ""
        BUILD_COMMAND ""
        INSTALL_COMMAND ""
    )
endif()


